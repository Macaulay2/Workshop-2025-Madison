doc ///
    Key
        EquivariantEhrhart
    Headline
        a package for equivariant Ehrhart theory of rational polytopes
    Subnodes
        -- Methods
        conjugacyClasses
        cycleTypeRepresentatives
        equivariantEhrhartSeries
        fixedPolytope
        generateGroup
        isSymmetric
        orbitPolytope
        representationRing
        -- Optional arguments
        OnlyListRepresentatives
        ReturnHStarList
        ReturnPartitionList
        ReturnTable
///

doc ///
    Key
        conjugacyClasses
        (conjugacyClasses, List)
        [conjugacyClasses, OnlyListRepresentatives]
        [conjugacyClasses, Verbose]
    Headline
        a function
    Usage
        C = conjugacyClasses G
    Inputs
        G : List
            a list containing all elements of a group generated by some matrices
        OnlyListRepresentatives => Boolean
            default value "false" returns a list of lists of classes; a value of
            "true" will return a single list of one representative for each conjugacy class
        Verbose => Boolean
            default value "false"; value of "true" will give extra output comments
    Outputs
        C : List
            a list of conjugacy classes of the group G
    Description
        Text
            A way to compute the conjugacy classes of a finte group
        Example
            g1 = matrix{{0,1,0,0},{0,0,1,0},{0,0,0,1},{1,0,0,0}}
            g2 = matrix{{0,0,1,0},{1,0,0,0},{0,0,0,1},{0,1,0,0}}
            G = generateGroup {g1,g2}
            conjugacyClasses G
    SeeAlso
        EquivariantEhrhart
        generateGroup
///

doc ///
    Key
        cycleTypeRepresentatives
        (cycleTypeRepresentatives, ZZ)
    Headline
        computes representatives for each cycle type in the symmetric group S_n
    Usage
        C = cycleTypeRepresentatives n
    Inputs
        n : ZZ
            size of the symmetric group for which we want to know the cycle type representatives
    Outputs
        : List
            one representative (written as a permutation matrix) from each cycle type of S_n
            in a list
    Description
        Text
            A way to compute a representative of each cycle type of the symmetric group
            on n elements. Representatives are given as n by n permutation matrices.
        Example
            cycleTypeRepresentatives 4
    SeeAlso
        EquivariantEhrhart
        (equivariantEhrhartSeries, Polyhedron)
///

-- doc ///
--     Key
--         equivariantEhrhartSeries
--     Headline
--         computes the equivariant Ehrhart series of a polytope
--     Subnodes
--         (equivariantEhrhartSeries, Polyhedron)
--         (equivariantEhrhartSeries, Polyhedron, Matrix)
--         (equivariantEhrhartSeries, Polyhedron, List)
-- ///
--
-- doc ///
--     Key
--         (equivariantEhrhartSeries, Polyhedron)
--     Headline
--         computes the equivariant Ehrhart series of a polytope
-- ///
--
-- doc ///
--     Key
--         (equivariantEhrhartSeries, Polyhedron, Matrix)
--     Headline
--         computes the equivariant Ehrhart series of a polytope
-- ///

doc ///
    Key
        equivariantEhrhartSeries
        (equivariantEhrhartSeries, Polyhedron)
        (equivariantEhrhartSeries, Polyhedron, List)
        (equivariantEhrhartSeries, Polyhedron, Matrix)
        [equivariantEhrhartSeries, ReturnHStarList]
        [equivariantEhrhartSeries, ReturnPartitionList]
        [equivariantEhrhartSeries, ReturnTable]
    Headline
        computes the equivariant Ehrhart series of a polytope
    Usage
        HF = equivariantEhrhartSeries(P)
        HM = equivariantEhrhartSeries(P, M)
        HG = equivariantEhrhartSeries(P, G)
    Inputs
        P : Polyhedron
            The polytope to calculate the equivariantEhrhartSeries for.
        G : List
            List of matrices that leaves the polytope invariant
        M : Matrix
            A matrix that leaves the polytope invariant
    Outputs
        HF : List
            The equivariant Ehrhart series of P with respect to symmetric group
            of coordinate permutations
        HG : RingElement
            The equivariant Ehrhart series of P with respect to the group
            generated by G
        HM : RingElement
            The evaluation of equivariant Ehrhart series of P with respect to
            the symmetric group of coordinate permutations
    Description
        Text
            There are three ways to use the function @TO equivariantEhrhartSeries@.
            The first way requires a polytope $P$ that is symmetric with respect to
            the full group of coordinate permutations. The equivariant Ehrhart series
            of $P$ with repect to the full symmetric group is computed. The result is
            a list consisting of: the $H^*$-polynomial; the character table of $S_n$;
            the cycle-types of the conjugacy classes of elements of $S_n$; the
            evaluations of $H^*$-polynomial at each conjugacy class.
        Example
            -- hypersimplex(2, 5)
            V = transpose matrix for s in subsets(5, 2) list for i from 0 to 4 list if isMember(i, s) then 1 else 0;
            P = convexHull V;
            vertices P

            netList equivariantEhrhartSeries P
        Text
            The second way to use the function is with a polytope $P$ and a matrix $M$.
            The function returns the Ehrhart series of the fixed polytope
            $P_M := \{x \in P : Mx = x \}$ of $P$ with respect to $M$. This coincides
            with the evaluation of the equivariant Ehrhart series of $P$ with respect
            to a group containing $M$.
        Example
            P = convexHull transpose matrix "1,1,0,0;0,1,1,0;0,0,1,1;1,0,0,1";
            vertices P
            M = matrix "0,0,0,1;1,0,0,0;0,1,0,0;0,0,1,0";
            equivariantEhrhartSeries(P, M)
        Text
            The third way to use the function is with a polytope $P$ and a list of
            matrices $G$ that leave the polytope invariant. The function computes
            the equivariant Ehrhart series of the polytope with respect to the group
            generated by the matrices. Currently, this is only available for cyclic
            groups, i.e., the list $G$ must have size one.
        Example
            P = convexHull transpose matrix "1,0,0,0;0,1,0,0;0,0,1,0"
            G = {matrix "0,1,0,0;1,0,0,0;0,0,1,0;0,0,0,1"}
            equivariantEhrhartSeries(P, G)

            -- for the hypersimplex(2,5)
            V = transpose matrix for s in subsets(5, 2) list for i from 0 to 4 list if isMember(i, s) then 1 else 0;
            P = convexHull V;
            G = {matrix "0,1,0,0,0;1,0,0,0,0;0,0,0,0,1;0,0,1,0,0;0,0,0,1,0"}; --an order 6 symmetry
            equivariantEhrhartSeries(P, G)
        Text
            The equivariant Ehrhart polynomial is defined a cyclotomic extension of $\QQ$
            with variables $X_1, \dots, X_m$ denoting the irreducible characters of
            the cyclic group generated by given matrix $g$. If the order of $g$ is $m$, then
            the character $X_i(g^j) = w^(ij)$ where $w$ is a primitive $m$th root of unity.
    SeeAlso
        EquivariantEhrhart
///

doc ///
    Key
        fixedPolytope
        (fixedPolytope, Polyhedron, Matrix)
    Headline
        computes the points of a polytope fixed by an invertible matrix
    Usage
        F = fixedPolytope(P,M)
    Inputs
        P : Polyhedron
        M : Matrix
            an invertable matrix with the same number of rows as the dimension of the ambient space of P
    Outputs
        F : Polyhedron
            the polyhderon formed from the subset of points of P that are fixed by g
    Description
        Text
            computes the points of a polytope fixed by an invertible matrix
        Example
            P = orbitPolytope(transpose matrix{{1/2,0,1}})
            fixedPolytope(P, matrix{{0,1,0},{1,0,0},{0,0,1}})
    SeeAlso
        EquivariantEhrhart
///

doc ///
    Key
        generateGroup
        (generateGroup, List)
        [generateGroup, Verbose]
    Headline
        a function
    Usage
        G = generateGroup gList
    Inputs
        gList : List
            a list of matrices which generate a finite group
            (verifying finite group generation left to the user)
        Verbose => Boolean
            default value "false"; value of "true" will give extra output comments
    Outputs
        G : List
            a list of all elements of the group generated by gList
    Description
        Text
            If gList contains one matrix, then this function returns
            a list of powers of that matrix, beginning with the identity.
        Example
            g = matrix{{0,1,0,0},{0,0,1,0},{0,0,0,1},{1,0,0,0}}
            generateGroup {g}
        Text
            If gList contains more than one matrices, then this function
            returns a list of all matrices generated by those matrices,
            in no particular order
        Example
            gList = {matrix{{0,1,0,0},{0,0,1,0},{0,0,0,1},{1,0,0,0}}, matrix{{0,0,1,0},{1,0,0,0},{0,0,0,1},{0,1,0,0}}}
            generateGroup gList
    SeeAlso
        EquivariantEhrhart
///

doc ///
    Key
        isSymmetric
        (isSymmetric, Polyhedron, Matrix)
        (isSymmetric, Polyhedron, List)
    Headline
        whether a polytope is symmetric
    Usage
        B = isSymmetric(P,M)
        B = isSymmetric(P,L)
    Inputs
        P : Polyhedron
        M : Matrix
            a square matrix with the same number of rows as the dimension of the ambient space of P
        L : List
            a list of square matrix with the same number of rows as the dimension of the ambient space of P
    Outputs
        B : Boolean
    Description
        Text
            checks that P is invariant under M or under each matrix in L
        Example
            P = orbitPolytope(transpose matrix{{1/2,0,1}})
            isSymmetric(P, matrix{{0,1,0},{1,0,0},{0,0,1}})
    SeeAlso
        EquivariantEhrhart
///

doc ///
    Key
        orbitPolytope
        (orbitPolytope, Matrix)
    Headline
        the polytope formed by the orbit of a point by Sn
    Usage
        P = orbitPolytope(M)
    Inputs
        M : Matrix
            a single column matrix: a point in QQ^n
    Outputs
        P : Polyhedron
            the polytope obtained from the orbit of a point under the action of the symmetric group
    Description
        Text
            computes the polytope obtained from the orbit of a point under the action of the symmetric group
        Example
            orbitPolytope(transpose matrix{{1/2,0,1}})
    SeeAlso
        EquivariantEhrhart
///

doc ///
    Key
        representationRing
        (representationRing, ZZ)
        [representationRing, ReturnTable]
    Headline
        representation ring of Sn over QQ
    Usage
        R = representationRing(n)
        (R,T) = representationRing(n, ReturnTable => true)
    Inputs
        n : ZZ
            the Sn that we compute the representation ring for
    Outputs
        R : Ring
            representation ring of Sn over QQ
        T : CharacterTable
            the symmetric group table of QQ[a_1 .. a_n]
    Description
        Text
            Creates the representation ring for Sn.
        Example
            representationRing 7
            representationRing(7, ReturnTable => true)
    SeeAlso
        EquivariantEhrhart
///
