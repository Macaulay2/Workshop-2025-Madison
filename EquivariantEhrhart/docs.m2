doc ///
    Key
        EquivariantEhrhart
    Headline
        a package for equivariant Ehrhart theory of rational polytopes
    Description
        Text
            Given a polytope $P \subseteq \RR^n$ that is invariant under
            the action of a finite subgroup $G \subseteq GL_n(\ZZ)$,
            each element $g \in G$ gives rise to a fixed polytope
            $P_g := \{x \in P : g(x) = x\}$. The
            the equivariant Ehrhart series is an object $EES_{P, G}(t)$ such
            that its evaluation at each group element $g \in G$ gives
            the usual Ehrhart series of the fixed polytope
            $EES_{P, G}(t)(g) = ES_{P_g}(t)$.

            This package provides methods for computing the equivariant
            Ehrhart series for polytopes under the action of the full
            symmetric group acting by cooridnate permutation, and the
            action of a cyclic group generated by a particular matrix.
            The main function of the package is @TO equivariantEhrhartSeries@.
    Acknowledgement
        This package was developed in part at the {\em Macaulay2} Workshop: Madison 2025.
        We thank the organizers Thomas Brazelton, Mahrud Sayrafi, and Thomas Yahl for organizing the workshop.
        We also thank Mike Stillman for helpful conversations during the development of the package.

    References
        @UL{
        {"[BR] M. Beck and S. Robins, ", HREF("https://doi.org/10.1007/978-1-4939-2969-6", EM "Computing the continuous discretely"), ", second edition, Undergraduate Texts in Mathematics, Springer, New York, 2015"},
        {"[S] A. Stapledon, ", HREF("https://doi.org/10.1016/j.aim.2010.10.019", EM "Equivariant Ehrhart theory"), ", Adv. Math. ", BOLD "226", " (2011), no. 4, 3622--3654"}
        }@

    Subnodes
        -- Methods
        cycleTypeRepresentatives
        equivariantEhrhartSeries
        fixedPolytope
        generateGroup
        isEffective
        isSymmetric
        orbitPolytope
        representationRing
        -- Optional arguments
        ReturnHStarList
        ReturnClassReps
        ReturnTable
///

-- doc ///
--     Key
--         conjugacyClasses
--         (conjugacyClasses, List)
--         [conjugacyClasses, OnlyListRepresentatives]
--         [conjugacyClasses, Verbose]
--     Headline
--         enumerates the conjugacy classes of a group
--     Usage
--         C = conjugacyClasses G
--     Inputs
--         G : List
--             a list containing all elements of a group generated by some matrices
--         OnlyListRepresentatives => Boolean
--             setting {\tt OnlyListRepresentatives=>true} will return a single list of one representative for each conjugacy class
--         Verbose => Boolean
--             setting {\tt Verbose=>true} true will give extra output comments
--     Outputs
--         C : List
--             a list of conjugacy classes of the group G
--     Description
--         Text
--             A way to compute the conjugacy classes of a finte group
--         Example
--             g1 = matrix{{0,1,0,0},{0,0,1,0},{0,0,0,1},{1,0,0,0}}
--             g2 = matrix{{0,0,1,0},{1,0,0,0},{0,0,0,1},{0,1,0,0}}
--             G = generateGroup {g1,g2}
--             conjugacyClasses G
--     SeeAlso
--         cycleTypeRepresentatives
--         generateGroup
-- ///

doc ///
    Key
        cycleTypeRepresentatives
        (cycleTypeRepresentatives, ZZ)
    Headline
        computes representatives for each cycle type in the symmetric group $S_n$
    Usage
        C = cycleTypeRepresentatives n
    Inputs
        n : ZZ
            size of the symmetric group for which we want to know the cycle type representatives
    Outputs
        : List
            one representative from each cycle type of $S_n$
    Description
        Text
            Computes a representative of each cycle type of the symmetric group, given as permutation matrices.
        Example
            cycleTypeRepresentatives 4
    SeeAlso
        generateGroup
///


doc ///
    Key
        equivariantEhrhartSeries
        (equivariantEhrhartSeries, Polyhedron)
        (equivariantEhrhartSeries, Polyhedron, List)
        (equivariantEhrhartSeries, Polyhedron, Matrix)
    Headline
        computes the equivariant Ehrhart series of a polytope
    Usage
        HF = equivariantEhrhartSeries(P)
        HM = equivariantEhrhartSeries(P, M)
        HG = equivariantEhrhartSeries(P, G)
    Inputs
        P : Polyhedron
            The polytope to calculate the equivariantEhrhartSeries for.
        G : List
            List of matrices that leaves the polytope invariant
        M : Matrix
            A matrix that leaves the polytope invariant
        ReturnHStarList => Boolean
            whether to return the list of $h^*$-polynomials of fixed polytopes
        ReturnClassReps => Boolean
            whether to return the list of cycle-types or class representatives
            of conjugacy classes of the cyclic group or $S_n$
        ReturnTable => Boolean
            whether to return the character table of the cyclic group or $S_n$
    Outputs
        HF : List
            The equivariant Ehrhart series of P with respect to symmetric group
            of coordinate permutations
        HG : RingElement
            The equivariant Ehrhart series of P with respect to the group
            generated by G
        HM : RingElement
            The evaluation of equivariant Ehrhart series of P with respect to
            the symmetric group of coordinate permutations
    Description
        Text
            There are three ways to use the function @TO equivariantEhrhartSeries@.
            The first way requires a polytope $P$ that is symmetric with respect to
            the full group of coordinate permutations. The equivariant Ehrhart series
            of $P$ with repect to the full symmetric group is computed. The result is
            a list consisting of: the $H^*$-polynomial; the character table of $S_n$;
            the cycle-types of the conjugacy classes of elements of $S_n$; the
            evaluations of $H^*$-polynomial at each conjugacy class.
        Example
            -- hypersimplex(2, 5)
            V = transpose matrix for s in subsets(5, 2) list for i from 0 to 4 list if isMember(i, s) then 1 else 0;
            P = convexHull V;
            vertices P

            netList equivariantEhrhartSeries P
        Text
            The second way to use the function is with a polytope $P$ and a matrix $M$.
            The function returns the Ehrhart series of the fixed polytope
            $P_M := \{x \in P : Mx = x \}$ of $P$ with respect to $M$. This coincides
            with the evaluation of the equivariant Ehrhart series of $P$ with respect
            to a group containing $M$.
        Example
            P = convexHull transpose matrix "1,1,0,0;0,1,1,0;0,0,1,1;1,0,0,1";
            vertices P
            M = matrix "0,0,0,1;1,0,0,0;0,1,0,0;0,0,1,0";
            equivariantEhrhartSeries(P, M)
        Text
            The third way to use the function is with a polytope $P$ and a list of
            matrices $G$ that leave the polytope invariant. The function computes
            the equivariant Ehrhart series of the polytope with respect to the group
            generated by the matrices. Currently, this is only available for cyclic
            groups, i.e., the list $G$ must have size one.
        Example
            P = convexHull transpose matrix "1,0,0,0;0,1,0,0;0,0,1,0"
            G = {matrix "0,1,0,0;1,0,0,0;0,0,1,0;0,0,0,1"}
            netList equivariantEhrhartSeries(P, G)

            -- for the hypersimplex(2,5)
            V = transpose matrix for s in subsets(5, 2) list for i from 0 to 4 list if isMember(i, s) then 1 else 0;
            P = convexHull V;
            G = {matrix "0,1,0,0,0;1,0,0,0,0;0,0,0,0,1;0,0,1,0,0;0,0,0,1,0"}; --an order 6 symmetry
            netList equivariantEhrhartSeries(P, G)
        Text
            The equivariant Ehrhart polynomial is defined over a cyclotomic extension of $\QQ$
            with variables $X_1, \dots, X_m$ denoting the irreducible characters of
            the cyclic group generated by given matrix $g$. If the order of $g$ is $m$, then
            the character $X_i(g^j) = w^{ij}$ where $w$ is a primitive $m$-th root of unity.
    SeeAlso
        isSymmetric
        ReturnHStarList
        ReturnClassReps
        ReturnTable
///

doc ///
    Key
        fixedPolytope
        (fixedPolytope, Polyhedron, Matrix)
    Headline
        computes the points of a polytope fixed by an invertible matrix
    Usage
        F = fixedPolytope(P,M)
    Inputs
        P : Polyhedron
        M : Matrix
            an invertable matrix with the same number of rows as the dimension of the ambient space of P
    Outputs
        F : Polyhedron
            the polyhderon formed from the subset of points of P that are fixed by g
    Description
        Text
            Given a polyhedron $P$ and a matrix $g$ (typically invertible), this function returns 
            the polytope of points of $P$ fixed by the multiplication action of $g$, that is, the polytope 
            $P_g := \{ x \in P \mid gx = x \}$.
        Example
            P = orbitPolytope(transpose matrix{{1/2,0,1}})
            vertices P
            PM = fixedPolytope(P, matrix permutation {1, 2, 3})
            vertices PM
    SeeAlso
        orbitPolytope
///

doc ///
    Key
        generateGroup
        (generateGroup, List)
        [generateGroup, Verbose]
    Headline
        enumerates the elements of a subgroup given subgroup generators
    Usage
        G = generateGroup gList
    Inputs
        gList : List
            a list of matrices which generate a finite group
            (verifying finite group generation is left to the user)
        Verbose => Boolean
            setting {\tt Verbose=>true} will give extra output comments
    Outputs
        G : List
            a list of all elements of the group generated by gList
    Description
        Text
            If {\tt gList} contains exactly one matrix {\tt g}, then this function returns
            the cyclic subgroup generated by {\tt g} as a @TO List@.
            If the order of {\tt g} is {\tt k}, then this list is is ordered {\tt \{id, g, g^2, ..., g^(k-1) \}}.
        Example
            g = matrix{
                {0,1,0,0},
                {0,0,1,0},
                {0,0,0,1},
                {1,0,0,0}};
            generateGroup {g}
        Text
            If {\tt gList} contains more than one matrix, then this function
            returns a @TO List@ containing the elements
            of the subgroup generated by the given matrices, in no particular order.
        Example
            gList = {matrix{{0,1,0,0},{0,0,1,0},{0,0,0,1},{1,0,0,0}}, matrix{{0,0,1,0},{1,0,0,0},{0,0,0,1},{0,1,0,0}}}
            generateGroup gList
    Caveat
        We make no effort to check that the cyclic subgroup generated by {\tt gList} is finite.
    SeeAlso
        cycleTypeRepresentatives
///


doc ///
    Key
        isSymmetric
        (isSymmetric, Polyhedron, Matrix)
        (isSymmetric, Polyhedron, List)
    Headline
        whether a polytope is symmetric
    Usage
        B = isSymmetric(P, M)
        B = isSymmetric(P, L)
    Inputs
        P : Polyhedron
        M : Matrix
            a square matrix with the same number of rows as the dimension of the ambient space of P
        L : List
            a list of square matrix with the same number of rows as the dimension of the ambient space of P
    Outputs
        B : Boolean
    Description
        Text
            checks that P is invariant under M or under each matrix in L
        Example
            P = orbitPolytope(transpose matrix{{1/2,0,1}})
            isSymmetric(P, matrix{{0,1,0},{1,0,0},{0,0,1}})
    SeeAlso
        fixedPolytope
///

doc ///
    Key
        orbitPolytope
        (orbitPolytope, Matrix)
    Headline
        the polytope formed by the orbit of a point by $S_n$
    Usage
        P = orbitPolytope(M)
    Inputs
        M : Matrix
            a single column matrix: a point in {\tt QQ^n}
    Outputs
        P : Polyhedron
            the polytope obtained from the orbit of a point under the action of the symmetric group
    Description
        Text
            Computes the polytope obtained from the orbit of a point under the action of the symmetric group.
        Example
            orbitPolytope(transpose matrix{{1/2,0,1}})
    SeeAlso
        fixedPolytope
///

doc ///
    Key
        representationRing
        (representationRing, ZZ)
        [representationRing, ReturnTable]
    Headline
        representation ring of $S_n$ over $\mathbb Q$
    Usage
        R = representationRing(n)
        (R, T) = representationRing(n, ReturnTable => true)
    Inputs
        n : ZZ
            the Sn that we compute the representation ring for
    Outputs
        R : Ring
            representation ring of $S_n$ over {\tt QQ}
        T : CharacterTable
            the symmetric group table of {\tt QQ[a_1 .. a_n]}
    Description
        Text
            Creates the representation ring for $S_n$.
        Example
            representationRing 7
            representationRing(7, ReturnTable => true)
    SeeAlso
        cycleTypeRepresentatives
        ReturnTable
///

doc ///
    Key
        isEffective
        (isEffective, RingElement)
    Headline
        method to check if an Ehrhart series is effective
    Usage
        result = isEffective(f)
    Inputs
        f : RingElement
            Ehrhart series
    Outputs
        result : Boolean
            whether or not {\tt f} is effective
    Description
        Text
            An Ehrhart series is effective if each coefficient is a genuine
            representation. In other words, each coefficient is a non-negative 
            integer combination of irreducible characters.
        Example
            P = convexHull matrix {
                {1, 1, 0, 1, 0, 0, 1, 0, 0, 0}, 
                {1, 0, 1, 0, 1, 0, 0, 1, 0, 0}, 
                {0, 1, 1, 0, 0, 1, 0, 0, 1, 0}, 
                {0, 0, 0, 1, 1, 1, 0, 0, 0, 1},
                {0, 0, 0, 0, 0, 0, 1, 1, 1, 1}};
            g = matrix {
                {0, 1, 0, 0, 0}, 
                {1, 0, 0, 0, 0}, 
                {0, 0, 0, 0, 1}, 
                {0, 0, 1, 0, 0}, 
                {0, 0, 0, 1, 0}};
            ES = (equivariantEhrhartSeries(P,{g}))_0
            isEffective ES
    SeeAlso
        equivariantEhrhartSeries
///



doc///
    Key
        ReturnHStarList
        [equivariantEhrhartSeries, ReturnHStarList]
    Headline
        return $h^*$-polynomials of fixed polytopes
    Description
        Text
            Set to {\tt true} to return the list of $h^*$ polynomials of fixed polytopes when 
            computing the equivariant Ehrhart series.
    SeeAlso
        equivariantEhrhartSeries
        ReturnClassReps
        ReturnTable
///

doc///
    Key
        ReturnClassReps
        [equivariantEhrhartSeries, ReturnClassReps]
    Headline
        return conjugacy class representatives
    Description
        Text
            Set to {\tt true} to return the conjugacy class representatives when 
            computing the equivariant Ehrhart series.
    SeeAlso
        equivariantEhrhartSeries
        ReturnHStarList
        ReturnTable
///

doc///
    Key
        ReturnTable
        [equivariantEhrhartSeries, ReturnTable]
    Headline
        return character table
    Description
        Text
            Set to {\tt true} to return the character table when 
            computing the equivariant Ehrhart series or representation ring.
    SeeAlso
        equivariantEhrhartSeries
        representationRing
        ReturnHStarList
        ReturnClassReps
///